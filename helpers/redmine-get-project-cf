#!/usr/bin/perl
use feature 'say';
use strict;
use warnings;
use List::MoreUtils qw/all/;
{
    package MyOpts;
    use Moo;
    use MooX::Options;
    option 'project' => (
        is       => 'ro',
        required => 1,
        doc      => 'project slug',
        format   => 's',
    );
    option 'auth_key' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'your auth key',
    );
    option 'server_url' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'the redmine url of the server',
    );
    option 'trace' => (
        is => 'ro',
        default => sub { 0 },
        doc => 'debug mode',
    );
    1;
};

my $opt = MyOpts->new_with_options;

use Redmine::API;
my $r = Redmine::API->new(
    'auth_key' => $opt->auth_key,
    'base_url' => $opt->server_url,
    'trace' => $opt->trace,
);

my $resp = $r->projects->project->get($opt->project, include => 'custom_fields');
my %cf = map { @$_{qw/name id/} } @{$resp->content->{project}->{custom_fields}};
if (all {defined} @cf{qw/GIT_REPOS GIT_PR GIT_RELEASE/}) {
    say "@cf{qw/GIT_REPOS GIT_PR GIT_RELEASE/}";
} else {
    exit 1
}