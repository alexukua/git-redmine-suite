#!/usr/bin/perl
use feature 'say';
use strict;
use warnings;
use utf8::all;
{
    package MyOpts;
    use Moo;
    use MooX::Options;
    option 'project' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'your project slug',
    );
    option 'auth_key' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'your auth key',
    );
    option 'server_url' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'the redmine url of the server',
    );
    option 'trace' => (
        is => 'ro',
        default => sub { 0 },
        doc => 'debug mode',
    );
    1;
};

my $opt = MyOpts->new_with_options;

use Redmine::API;
my $r = Redmine::API->new(
    'auth_key' => $opt->auth_key,
    'base_url' => $opt->server_url,
    'trace' => $opt->trace,
);

my $resp = $r->projects->project->get($opt->project, include => 'members');
my $content = $resp->content->{project}->{members} // [];
say "The list of users for the project '", $opt->project, "' : ";
for my $member(sort { $a->{name} cmp $b->{name} } @$content) {
    say sprintf("    %-5d - %-30s", $member->{id}, $member->{name});
}
