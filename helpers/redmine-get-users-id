#!/usr/bin/env perl
use feature 'say';
use strict;
use warnings;
{
    package MyOpts;
    use Moo;
    use MooX::Options;
    option 'project' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'your project slug',
    );
    option 'auth_key' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'your auth key',
    );
    option 'server_url' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'the redmine url of the server',
    );
    option 'trace' => (
        is => 'ro',
        default => sub { 0 },
        doc => 'debug mode',
    );
    1;
};

my $opt = MyOpts->new_with_options;

use Redmine::API;
my $r = Redmine::API->new(
    'auth_key' => $opt->auth_key,
    'base_url' => $opt->server_url . '/projects/' . $opt->project,
    'trace' => $opt->trace,
);

my $resp = $r->memberships->list->all();
my $content = $resp->content->{memberships};
say "The list of users for the project '", $opt->project, "' : ";
use utf8;
for my $member(@$content) {
    next if !exists $member->{user};
    utf8::encode($member->{user}->{name});
    say sprintf("    %-5d - %-30s [%s]", $member->{user}->{id}, $member->{user}->{name}, join(', ', map {utf8::encode($_->{name}); $_->{name}} @{$member->{roles}}));
}
