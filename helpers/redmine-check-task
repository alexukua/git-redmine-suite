#!/usr/bin/perl
use feature 'say';
use strict;
use warnings;
{
    package MyOpts;
    use Moo;
    use MooX::Options;
    option 'id' => (
        is       => 'ro',
        required => 1,
        doc      => 'id of the task',
        format   => 'i',
    );
    option 'auth_key' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'your auth key',
    );
    option 'server_url' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'the redmine url of the server',
    );
    option 'trace' => (
        is => 'ro',
        default => sub { 0 },
        doc => 'debug mode',
    );
    option 'assigned_to_id' => (
        is       => 'ro',
        required => 1,
        doc      => 'task assigned to',
        format   => 'i',
    );
    option 'status_ids' => (
        is => 'ro',
        required => 1,
        format => 'i@',
        autosplit => ',',
        doc => 'valid status',
    );
    option 'message' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'message if fail',
    );
    1;
};

my $opt = MyOpts->new_with_options;

use Redmine::API;
my $r = Redmine::API->new(
    'auth_key' => $opt->auth_key,
    'base_url' => $opt->server_url,
    'trace' => $opt->trace,
);

my $id = $opt->id;
my @status_ids = @{$opt->status_ids};
my $resp = $r->issues->issue->get($id);
my $issue = $resp->content->{issue};
my $is_valid = 1;
$is_valid = 0 if (!$issue->{assigned_to} || $issue->{assigned_to}->{id} ne $opt->assigned_to_id);
my $is_status_valid = 0;
for my $status_id(@status_ids) {
    $is_status_valid = 1 if ($status_id eq $issue->{status}->{id});
}
$is_valid = 0 if !$is_status_valid;
my $assigned_to = $issue->{assigned_to} ? $issue->{assigned_to}->{name} : 'nobody';
my $status = $issue->{status}->{name};
if (!$is_valid) {
    my $message = $opt->message;
    $message = <<__EOF__
This task has not the right status in redmine.
It is assigned to "$assigned_to" and as the status "$status".

Please check that the task is assigned properly, and has the expected status.
__EOF__
    if $message eq 'DEFAULT';
    say $message;
    exit 1;
}
