#!/usr/bin/perl
use feature 'say';
use strict;
use warnings;
use Encode;
{
    package MyOpts;
    use Moo;
    use MooX::Options;
    option 'auth_key' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'your auth key',
    );
    option 'server_url' => (
        is => 'ro',
        format => 's',
        required => 1,
        doc => 'the redmine url of the server',
    );
    option 'trace' => (
        is => 'ro',
        default => sub { 0 },
        doc => 'debug mode',
    );
    1;
};

my $opt = MyOpts->new_with_options;

use Redmine::API;
my $r = Redmine::API->new(
    'auth_key' => $opt->auth_key,
    'base_url' => $opt->server_url,
    'trace' => $opt->trace,
);

my $resp = $r->projects->list->all(limit => 1000);
my $content = $resp->content->{projects};
say "List of all project and slugs : ";
for my $project(sort { $a->{identifier} cmp $b->{identifier} } @$content) {
    $project->{name} = encode('UTF8', $project->{name});
    say sprintf("    %-40s ( %s )", @$project{qw/identifier name/});
}
